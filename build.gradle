/**
 * 将插件放到查找路径中。如果插件不存在，需要将buildscript注释掉。编译生成插件后，再取消注释。
 */
buildscript {
    repositories {
        flatDir {
            name 'localRepository'
            dir "$projectDir/build/target/libs"
        }
    }
    dependencies {
        classpath(group:'com.windfish.gradle',name:'lazyfish',version: '1.0') {
            changing = true
        }
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.10.0"
}
pluginBundle {
    website = 'https://github.com/Ken-W-P-Huang/lazyfish'
    vcsUrl = 'https://github.com/Ken-W-P-Huang/lazyfish.git'
    tags = ['BC/S','node','grunt']
}

gradlePlugin {
    plugins {
        lazyFishPlugin {
            id = 'com.windfish.gradle.lazyfish'
            displayName = 'BC/S Plugin'
            description = 'facilitate Browser Client/Server Develop'
            implementationClass = 'com.windfish.gradle.LazyFishPlugin'
        }
    }
}

group 'com.windfish.gradle'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile 'junit:junit:4.+'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

uploadArchives{
    repositories{
        flatDir{
            name "localRepository"
            dir "$projectDir/build/target/libs"
        }
    }
}





